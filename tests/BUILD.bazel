load("@bazel_skylib//rules:build_test.bzl", "build_test")
load("@rules_shell//shell:sh_test.bzl", "sh_test")
load("//:private.bzl", "execution_platforms")

# List of binaries supported on all 4 platforms: (Linux + Mac) Ã— (x86-64 + Arm64).
# `{}` is a placeholder for the platform name. See `MODULE.bazel`.
binaries = [
    "@kubectl.{}//file",
    "@etcd.{}//:etcd",
    "@helm.{}//:helm",
    "@kustomize.{}//:kustomize",
    "@crictl.{}//:crictl",
    "@kops.{}//file",
    "@operator-sdk.{}//file",
    "@controller-gen.{}//file",
    "@oras.{}//:oras",
    "@crane.{}//:crane",
    "@kind.{}//file",
    "@cloud-provider-kind.{}//:cloud-provider-kind",
    "@grpcurl.{}//:grpcurl",
]

# These binaries are only available on certain platforms,
# so hardcode all possible values.
hardcoded_binaries = [
    "@kube-apiserver.x86_64-linux//file",
    "@kube-apiserver.aarch64-linux//file",
    "@gateway-api-crds//file",
    "@envoy-gateway-crds//file",
]

# Download all remote files for all platforms
# to make sure the checksums are correct in MODULE.bazel.
build_test(
    name = "fetch-all-remotes",
    targets = [
        binary.format(platform)
        for binary in binaries
        for platform in execution_platforms
    ] + hardcoded_binaries,
)

sh_test(
    name = "image-export-test",
    srcs = ["image-export-test.sh"],
    args = ["$(location //:envoy-gateway-helm-export)"],
    data = ["//:envoy-gateway-helm-export"],
)
