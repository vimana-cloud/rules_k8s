module(
    name = "rules_k8s",
    version = "0.0.1",
)

bazel_dep(name = "buildifier_prebuilt", version = "8.2.1", dev_dependency = True, repo_name = "buildtools")

bazel_dep(name = "aspect_bazel_lib", version = "2.21.2")
bazel_dep(name = "bazel_skylib", version = "1.8.2")
bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "rules_oci", version = "2.2.6")
bazel_dep(name = "rules_shell", version = "0.6.1")

# Supported execution platforms for pre-built binaries.
# TODO: Figure out how to dedupe these lists with `private.bzl`.
execution_platforms = [
    "aarch64-linux",
    "aarch64-macos",
    "x86_64-linux",
    "x86_64-macos",
]

# https://github.com/kubernetes/kubernetes/releases
kubernetes_version = "1.34.2"

# To find the best-tested version of etcd for a given version of K8s,
# check the changelog, e.g.
# https://github.com/kubernetes/kubernetes/blob/master/CHANGELOG/CHANGELOG-1.34.md
etcd_version = "3.6.5"

# https://github.com/helm/helm/releases
helm_version = "4.0.1"

# https://github.com/kubernetes-sigs/gateway-api/releases
# Must be compatible with `envoy_gateway_version`: https://gateway.envoyproxy.io/news/releases/matrix/
gateway_api_version = "1.4.0"

# https://github.com/envoyproxy/gateway/releases
# Must be compatible with `gateway_api_version`: https://gateway.envoyproxy.io/news/releases/matrix/
# Must match `envoy_gateway_helm_digest`.
envoy_gateway_version = "1.6.0"

# https://hub.docker.com/r/envoyproxy/gateway-helm/tags
# Must match `envoy_gateway_version`.
envoy_gateway_helm_digest = "sha256:905eced000d4b2acb78f802f5d03af32a08d30478808c20d522ffa735476bc5d"

# https://github.com/containernetworking/plugins/releases
cni_plugins_version = "1.8.0"

# https://github.com/kubernetes-sigs/kustomize/releases
kustomize_version = "5.8.0"

# https://github.com/kubernetes-sigs/cri-tools/releases
cri_tools_version = "1.34.0"

# https://github.com/kubernetes/kops/releases
kops_version = "1.34.1"

# https://github.com/operator-framework/operator-sdk/releases
operator_sdk_version = "1.42.0"

# https://github.com/kubernetes-sigs/controller-tools/releases
controller_tools_version = "0.19.0"

# https://github.com/kubernetes/minikube/releases
# https://github.com/vimana-cloud/minikube/releases
minikube_version = "1.36.0-workd.5"

# https://github.com/google/go-containerregistry/releases
crane_version = "0.20.7"

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_file = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_file")

oci = use_extension("@rules_oci//oci:extensions.bzl", "oci")

k8s_platforms = {
    "aarch64-linux": "linux/arm64",
    "aarch64-macos": "darwin/arm64",
    "x86_64-linux": "linux/amd64",
    "x86_64-macos": "darwin/amd64",
}

[http_file(
    name = "kubectl-" + exe_platform,
    executable = True,
    integrity = {
        "aarch64-linux": "sha384-JYgFYJ14HVGOSBPewpmfbyl0Hf0VSkcACUSw7Urih3Ll1H19U+qJv1GpPBnpKmTh",
        "aarch64-macos": "sha384-EuRYfuVKOjvhrCs7S83ADnqrhWmveuhIq5DLL4Ic2l6s67LCIHSMngDbrdzYzRwl",
        "x86_64-linux": "sha384-LqR1R+plM9GQaWitVOQaN+FCRC78C4dfcvyqU+H7k6z5zbudEBZ3yZLmcqU1pIuL",
        "x86_64-macos": "sha384-83Eby9x8NSIRymu29BIfPiNc6vKqOqFSerlyHpWV5oggzUnNCqZDSVrjnjZvMe5q",
    }[exe_platform],
    url = "https://dl.k8s.io/v{version}/bin/{platform}/kubectl".format(
        platform = k8s_platforms[exe_platform],
        version = kubernetes_version,
    ),
) for exe_platform in execution_platforms]

[http_file(
    name = "kube-apiserver-" + exe_platform,
    executable = True,
    integrity = {
        "aarch64-linux": "sha384-Yc8HS/IbCTbKk0apo9vFrSOSj1JJkxl/9f01W1MRWNarERt3JYqwQndQj67xAe9I",
        "aarch64-macos": None,
        "x86_64-linux": "sha384-dhgsbE+UlyBotFvNfibYyFBLZbbGARkFcfLfOZLNZDzn1lLSI/AMjkZ3hOZlmlUL",
        "x86_64-macos": None,
    }[exe_platform],
    url = "https://dl.k8s.io/v{version}/bin/{platform}/kube-apiserver".format(
        platform = k8s_platforms[exe_platform],
        version = kubernetes_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "etcd-" + exe_platform,
    build_file_content = """
package(
    default_visibility = ["//visibility:public"],
)
alias(
    name = "etcd",
    actual = "etcd-v{version}-{platform}/etcd",
)
alias(
    name = "etcdctl",
    actual = "etcd-v{version}-{platform}/etcdctl",
)
alias(
    name = "etcdutl",
    actual = "etcd-v{version}-{platform}/etcdutl",
)
""".format(
        platform = {
            "aarch64-linux": "linux-arm64",
            "aarch64-macos": "darwin-arm64",
            "x86_64-linux": "linux-amd64",
            "x86_64-macos": "darwin-amd64",
        }[exe_platform],
        version = etcd_version,
    ),
    integrity = {
        "aarch64-linux": "sha384-XsMiUgYJB2Wd9TCKs15sTGA2BZKJQsSr9Z69Kmteq8olxTSTNkjLrngXL9mIQ01s",
        "aarch64-macos": "sha384-7YNgLxSnaY9uSPxYxk/0iOm6pwLQOlcqi4wOnffAGSw1795mk8qCYOgEtOYo5WKq",
        "x86_64-linux": "sha384-I9jl9x9e3jG5uTuGNyG//y9wFufstxhLr4DNsw9ipUjvwQkMV/7sIOVsO5KkLDDa",
        "x86_64-macos": "sha384-GDr6dAQFHM+JN8kvOTBSUISc3295JulHq7d6uOo3JFr6/w9QkWSaS1QPMV1u1+iF",
    }[exe_platform],
    url = "https://github.com/etcd-io/etcd/releases/download/v{version}/etcd-v{version}-{platform}".format(
        platform = {
            "aarch64-linux": "linux-arm64.tar.gz",
            "aarch64-macos": "darwin-arm64.zip",
            "x86_64-linux": "linux-amd64.tar.gz",
            "x86_64-macos": "darwin-amd64.zip",
        }[exe_platform],
        version = etcd_version,
    ),
) for exe_platform in execution_platforms]

helm_platforms = {
    "aarch64-linux": "linux-arm64",
    "aarch64-macos": "darwin-arm64",
    "x86_64-linux": "linux-amd64",
    "x86_64-macos": "darwin-amd64",
}

[http_archive(
    name = "helm-" + exe_platform,
    build_file_content = """
alias(
    name = "helm",
    actual = "{platform}/helm",
    visibility = ["//visibility:public"],
)
""".format(platform = helm_platforms[exe_platform]),
    integrity = {
        "aarch64-linux": "sha384-1zlI7+GlREfxvPdaDvcVD5t1Mi+iLPLNv1qAafLoCG9ojx/9GBW8FdMhJkbR8yj8",
        "aarch64-macos": "sha384-McGgLY/RHWXT55h5RzQHWqYYKEpekZQmx+xa2r7lGP9HPoEBA4u1z0sfYpKEd3/p",
        "x86_64-linux": "sha384-UUv9ZBaH1z00NGPrW4G+k2bxbcaHd92yt9BRmePShBxSZ4z7MYvMWNDer5lFSnew",
        "x86_64-macos": "sha384-+CSYgUdS7FrYn1Nnk8agn93t6zYUeQHgbvYXr8uYodTVuco9l09kvvU+mziY38JS",
    }[exe_platform],
    url = "https://get.helm.sh/helm-v{version}-{platform}.tar.gz".format(
        platform = helm_platforms[exe_platform],
        version = helm_version,
    ),
) for exe_platform in execution_platforms]

http_file(
    name = "gateway-api-crds",
    integrity = "sha384-qFJe9RR6XBEZM7cGg2JoqhJWq8U0/uqfSLiWbazmec/+p6G9w9Uu/zW8BLQdIwXH",
    url = "https://github.com/kubernetes-sigs/gateway-api/releases/download/v{version}/standard-install.yaml".format(
        version = gateway_api_version,
    ),
)

http_file(
    name = "envoy-gateway-crds",
    integrity = "sha384-ET3PwZPkkfm70NdY15Al19v9fjDq/I/jJVCSNDomCnIgTxeC1dgQ9ODY3X29HFw2",
    url = "https://github.com/envoyproxy/gateway/releases/download/v{version}/envoy-gateway-crds.yaml".format(
        version = envoy_gateway_version,
    ),
)

oci.pull(
    name = "envoy-gateway-helm",
    digest = envoy_gateway_helm_digest,
    image = "docker.io/envoyproxy/gateway-helm",
)

# CNI plugins end up in the node Docker image, so we only need Linux / x86-64.
http_archive(
    name = "cni-plugins-x86_64-linux",
    build_file_content = """
exports_files(
    ["host-local"],
    visibility = ["//visibility:public"],
)
""",
    integrity = "sha384-vFbTEvfxOqOdFCTx07oHBmQXHATIoRmgQw8rVax2g1b7HvuBjGsqj+RnHxHO+6KT",
    url = "https://github.com/containernetworking/plugins/releases/download/v{version}/cni-plugins-linux-amd64-v{version}.tgz".format(
        version = cni_plugins_version,
    ),
)

[http_archive(
    name = "kustomize-" + exe_platform,
    build_file_content = """
exports_files(
    ["kustomize"],
    visibility = ["//visibility:public"],
)
""",
    integrity = {
        "aarch64-linux": "sha384-HT6hk+i2vLQrinVboYeZ4cPMNwz4kwnFzI/+jWy4Ul/+LXaJ5I5CaLWB+A13ze4I",
        "aarch64-macos": "sha384-xJqaTV1cSo8d6RhIhMhGHMXFaeL1SGuMPU34+rckfY/BeLI63Tb3UBhbw/YNXFzZ",
        "x86_64-linux": "sha384-jAUP7VV5xC6oa0Z5LESS1zZNQ057hsRz2+BaWZN6iAx12El+sqnbj8jfZ+wrkobU",
        "x86_64-macos": "sha384-8+pc6GTy0KulYYzFAACRSTTlCoMS0s6o7qESqDN8IpoE8QbfmdM/rtVpbvGhjSMk",
    }[exe_platform],
    url = "https://github.com/kubernetes-sigs/kustomize/releases/download/kustomize%2Fv{version}/kustomize_v{version}_{platform}.tar.gz".format(
        platform = {
            "aarch64-linux": "linux_arm64",
            "aarch64-macos": "darwin_arm64",
            "x86_64-linux": "linux_amd64",
            "x86_64-macos": "darwin_amd64",
        }[exe_platform],
        version = kustomize_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "crictl-" + exe_platform,
    build_file_content = """
exports_files(
    ["crictl"],
    visibility = ["//visibility:public"],
)
""",
    integrity = {
        "aarch64-linux": "sha384-RYlY4kmfNgNh+DE8GQBUZWKAglyy5uRl3kJuSSZx2Il0TCEYOHcP0fDiXZhi2i6t",
        "aarch64-macos": "sha384-lWs9vYjYGp6CZIf/5YKxAvea4VC/7AUHVqM/pjc8MQ/HlBw5WRj/oiTtuuH6n+Dt",
        "x86_64-linux": "sha384-khLyVXuZCk5ZF+BZh1syYyhBs2rdILzVvhMV0nW9/DgU68Q17KJVVdKbqdIPdKMu",
        "x86_64-macos": "sha384-cwc9e67m2dVaxBc2Zbtf1qsMoQDvDaogV3LpX5s9DIjbfVLcjgJABjBlV0eRs5Hg",
    }[exe_platform],
    url = "https://github.com/kubernetes-sigs/cri-tools/releases/download/v{version}/crictl-v{version}-{platform}.tar.gz".format(
        platform = {
            "aarch64-linux": "linux-arm64",
            "aarch64-macos": "darwin-arm64",
            "x86_64-linux": "linux-amd64",
            "x86_64-macos": "darwin-amd64",
        }[exe_platform],
        version = cri_tools_version,
    ),
) for exe_platform in execution_platforms]

[http_file(
    name = "kops-" + exe_platform,
    executable = True,
    integrity = {
        "aarch64-linux": "sha384-66f3DTHDxoriTbJUXeYkEJf1x0MbuIZpVCY21SFaRWoVektfxSlMTxfOmhkDMJAQ",
        "aarch64-macos": "sha384-VqVMCIgK+NldAH0l2ldHKaOvxSewjxvWuSU7bV2W+1XV0OgBKtRd5HhVTZHzERTL",
        "x86_64-linux": "sha384-VOvBxxH5Yp8sqMM2jcKlvbqwbJfHU1sskhvDMkkf9HrHscQvVD6ptpFNgTo9e126",
        "x86_64-macos": "sha384-C/AKz31bEjjvsx4s6gkjq85Lob4ymEwWtcIsuUadJGND+3ZujC/tRezLmMPZKYck",
    }[exe_platform],
    url = "https://github.com/kubernetes/kops/releases/download/v{version}/kops-{platform}".format(
        platform = {
            "aarch64-linux": "linux-arm64",
            "aarch64-macos": "darwin-arm64",
            "x86_64-linux": "linux-amd64",
            "x86_64-macos": "darwin-amd64",
        }[exe_platform],
        version = kops_version,
    ),
) for exe_platform in execution_platforms]

[http_file(
    name = "operator-sdk-" + exe_platform,
    executable = True,
    integrity = {
        "aarch64-linux": "sha384-ZYjrE8CBkoIcN+iTOpP6J51I1RcG5fQ6kkz9T+dJ5rnirSH+1WN0j46JOCRdJv4/",
        "aarch64-macos": "sha384-WfX4NeZiSkpP0UhOTsmsCl7uqovpt/YHxkCoNWpe0nUjwtzW+qS4sTXdvSa8dvDv",
        "x86_64-linux": "sha384-RnuObky1U2KAjMBpLZfD2nniqkUmCwKkzX2IRsytDz5mqrGFM17EAnMhWhgzRBrw",
        "x86_64-macos": "sha384-C/DEReaA/sN8iWuGmxohmYA0s/ty7wG87w+WteaXpJd4RG9AEshfr8kgRu4NQBvq",
    }[exe_platform],
    url = "https://github.com/operator-framework/operator-sdk/releases/download/v{version}/operator-sdk_{platform}".format(
        platform = {
            "aarch64-linux": "linux_arm64",
            "aarch64-macos": "darwin_arm64",
            "x86_64-linux": "linux_amd64",
            "x86_64-macos": "darwin_amd64",
        }[exe_platform],
        version = operator_sdk_version,
    ),
) for exe_platform in execution_platforms]

[http_file(
    name = "controller-gen-" + exe_platform,
    executable = True,
    integrity = {
        "aarch64-linux": "sha384-GlPisQ9VwsIdZ4hqLSjl+fMRTJm+faUPgTWyyTPa5DNg7nCL1rDDVORn2/esluqG",
        "aarch64-macos": "sha384-iQSDVVU9iiUeDlW1E0gi+EweyZKcCXVO3ABkmvpn7GLRPTmqwMEHILl6eLrwAoA6",
        "x86_64-linux": "sha384-PmbZLpovXiraxmmokhNoZ9XITn2eYqSs2u8erFzq4eX8BJsA+QbBaETREuYP+15+",
        "x86_64-macos": "sha384-RWp3N0j++AFWbkpocYTXAIcvcFZ4M52rJNrGytiL5UKuSqyxmIiszxnHvJrKX/np",
    }[exe_platform],
    url = "https://github.com/kubernetes-sigs/controller-tools/releases/download/v{version}/controller-gen-{platform}".format(
        platform = {
            "aarch64-linux": "linux-arm64",
            "aarch64-macos": "darwin-arm64",
            "x86_64-linux": "linux-amd64",
            "x86_64-macos": "darwin-amd64",
        }[exe_platform],
        version = controller_tools_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "minikube-" + exe_platform,
    build_file_content = """
exports_files(
    ["minikube"],
    visibility = ["//visibility:public"],
)
""",
    integrity = {
        "aarch64-linux": "sha384-EC7gXbQqYB7FTTJ5WrE+wN+uBOtoieHN/itbVniC+QNMYTroT2lisxzupsbQgno1",
        "aarch64-macos": "sha384-xFeNol9IXUEOIceg9E5ChnJeac/JO8v7QMw/l4XO+Z6F5VbGp0r540ej5g/OZJCq",
        "x86_64-linux": "sha384-tyMRuSRqcaPXH3a22yWU0uBTwMViCUW6q1ssTBThxB8FXZKVI4r9Uh7/AS+3TjEA",
        "x86_64-macos": "sha384-NV64MFknFprMoKB0LT76lUfp+AscKweCQdSpeODhhOijwWA/3vyWKtB4JwB3pW2d",
    }[exe_platform],
    url = "https://github.com/vimana-cloud/minikube/releases/download/v{version}/minikube-{platform}.tar.gz".format(
        platform = {
            "aarch64-linux": "linux-arm64",
            "aarch64-macos": "darwin-arm64",
            "x86_64-linux": "linux-amd64",
            "x86_64-macos": "darwin-amd64",
        }[exe_platform],
        version = minikube_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "crane-" + exe_platform,
    build_file_content = """
exports_files(
    ["crane"],
    visibility = ["//visibility:public"],
)
""",
    integrity = {
        "aarch64-linux": "sha384-KVuVHnH5XxTv4LlCADB+pHnLmoRw7Rhz/gfSVLQguWXsh2hM24/ELbzsBzhegvTu",
        "aarch64-macos": "sha384-EMXlKoGABMKsFZ5y8wN39X8J/FPcmUR6hLlnIedu1J+ZsxwcCfF64gMvfPE2xOiR",
        "x86_64-linux": "sha384-7PVWOmKvdH7KD1lRPosQfedN4Dy2BxGYNh5MMFqKpu9Opm44G2y8dM/8vR/ZwKiC",
        "x86_64-macos": "sha384-Lp3L3RQ85K9/3lRrdZy2ofKCeb6qrsfFfiZrOwcok6Osl6wBaoQ6Ubjv+CH2FUbZ",
    }[exe_platform],
    url = "https://github.com/google/go-containerregistry/releases/download/v{version}/go-containerregistry_{platform}.tar.gz".format(
        platform = {
            "aarch64-linux": "Linux_arm64",
            "aarch64-macos": "Darwin_arm64",
            "x86_64-linux": "Linux_x86_64",
            "x86_64-macos": "Darwin_x86_64",
        }[exe_platform],
        version = crane_version,
    ),
) for exe_platform in execution_platforms]

use_repo(
    oci,
    "envoy-gateway-helm",
)
