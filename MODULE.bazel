module(
    name = "rules_k8s",
    version = "0.0.1",
)

bazel_dep(name = "bazel_skylib", version = "1.7.1")
bazel_dep(name = "platforms", version = "0.0.10")

bazel_dep(name = "buildifier_prebuilt", version = "7.3.1", dev_dependency = True, repo_name = "buildtools")

# Supported execution platforms for pre-built binaries.
# TODO: Figure out how to dedupe this list with `private.bzl`.
x86_64_platforms = [
    "x86_64-linux",
    "x86_64-macos",
]

aarch64_platforms = [
    "aarch64-linux",
    "aarch64-macos",
]

execution_platforms = x86_64_platforms + aarch64_platforms

# https://github.com/kubernetes/kubernetes/releases
kubernetes_version = "v1.31.2"

kubectl_integrity = {
    "aarch64-linux": "sha384-EI3XWAb35lxGxdpopPjCsuy3IjUcgnGgQPld4XG+BQnjlV9/OTR8B3JmVSwJoMiM",
    "aarch64-macos": "sha384-g3X1owXXsQJYfrHDU7h5DxGin83nWwr1cNuWo3wTvtgZgh/H4GEoV7aWEPn/fWms",
    "x86_64-linux": "sha384-d1TmGRQ3gIee3Ya0YjtelVtr/iSRXLdgsbn97qlm1U9oexLj8uHm6xn1QmVkiDmz",
    "x86_64-macos": "sha384-7DXAvTpWgUW+vg0m02pcTZ8GIdVxOw3L8dT73xPi7g6ZYZ8yD6CkkG+f5iXhK/sT",
}

# https://github.com/istio/istio/releases
istio_version = "1.23.5"

istio_integrity = {
    "aarch64-linux": "sha384-YEOJzfY8nllLjlPHQzKu1c8Dz8csLJrh79ItPLCgBq26ZfbkmzMjUZT5YMsEKAiY",
    "aarch64-macos": "sha384-7T2K4EGdGL1DoUevBreMcPSg2zAdf2D2fHAVcyGFnBDd1k9qTSOJKXpgCGbklQuz",
    "x86_64-linux": "sha384-/1jw6GBYFfm5klFl+UjQNFRiQEKf8vEF4Y4OvVkR22YW7KjF/9UmDpi5Lvc4kM59",
    "x86_64-macos": "sha384-gKqehKzsn+Sm8z9q86r7fYhxf+1FMZCgkDyn2RoxEhc6TsbqLmyRYY5CheO58R6x",
}

# https://github.com/kubernetes-sigs/kustomize/releases
kustomize_version = "v5.5.0"

kustomize_integrity = {
    "aarch64-linux": "sha384-LCSt1E04vEP5og6Y4AcTyJdpNHips3VPAf3Ud55TkU0Tg8d0eYnlQJTNINl7iVBb",
    "aarch64-macos": "sha384-p7xFnsqGklQs7uRlgwMRjwS7iODKghZDrvNWoLerOQbOa2fgd2froIL944iva6p8",
    "x86_64-linux": "sha384-iD1YvxIfYR9N+Hi+igw8GXeeoWuMNqAUOqckRkDJJEWp1r7dBEnAynR+JPn4DZbT",
    "x86_64-macos": "sha384-2DQtUwEUOGKeTJbWKQahWjk9C9MBCndjsP0EASrNozBBK9I7JFlwUHpyY7I5zcmy",
}

# https://github.com/kubernetes-sigs/cri-tools/releases
cri_tools_version = "1.32.0"

crictl_integrity = {
    "aarch64-linux": "sha384-IdQKFIqnDJf7FW0ghTx/e5C8rEUN/CKrOgUp+i2hRhwuyo8cwP92xzRUPgk099Aa",
    "aarch64-macos": "sha384-F9X0pfy11Sy8eiOuXP8+z8c0dxyKLIZJ0VGTyaERF+2HgKonf5qWBNPtPY+zwBb+",
    "x86_64-linux": "sha384-QeIQzwO9jKnAxfyGfY1caKIr9nX6WpQ9lxNsVHvFag57Pq5W7RUlG5QCKg+czTlC",
    "x86_64-macos": "sha384-1FBOnDDMEP45IH1twFgYW2YdHIcRCseeiEc6cuW7av9P56yO3rJ9xXiWyvXTssxe",
}

# https://github.com/vimana-cloud/minikube/releases
minikube_version = "1.35.0-workd.3"

minikube_integrity = {
    "x86_64-linux": "sha384-SxhZxU+27v71WOHfvyDhvHAPrsJ9F4+0kKtOaELecjanuQdDgFF8Yy7dwq68/T3e",
    "x86_64-macos": "sha384-lQH3ixrQpMZfrgYtlGoSxniM6H9NKtbuJ/taTsxASIAvhSTXsu7G2gd7dfbPLuRC",
}

# Manually download prebuilt binaries:

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_file = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_file")

[http_file(
    name = "kubectl-" + exe_platform,
    executable = True,
    integrity = kubectl_integrity[exe_platform],
    url = "https://dl.k8s.io/release/{version}/bin/{platform}/kubectl".format(
        platform = {
            "aarch64-linux": "linux/arm64",
            "aarch64-macos": "darwin/arm64",
            "x86_64-linux": "linux/amd64",
            "x86_64-macos": "darwin/amd64",
        }[exe_platform],
        version = kubernetes_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "istio-" + exe_platform,
    build_file_content = """
alias(
    name = "istioctl",
    actual = "istio-{version}/bin/istioctl",
    visibility = ["//visibility:public"],
)
""".format(version = istio_version),
    integrity = istio_integrity[exe_platform],
    url = "https://github.com/istio/istio/releases/download/{version}/istio-{version}-{platform}.tar.gz".format(
        platform = {
            "aarch64-linux": "linux-arm64",
            "aarch64-macos": "osx-arm64",
            "x86_64-linux": "linux-amd64",
            "x86_64-macos": "osx-amd64",
        }[exe_platform],
        version = istio_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "kustomize-" + exe_platform,
    build_file_content = """
exports_files(
    ["kustomize"],
    visibility = ["//visibility:public"],
)
""",
    integrity = kustomize_integrity[exe_platform],
    url = "https://github.com/kubernetes-sigs/kustomize/releases/download/kustomize%2F{version}/kustomize_{version}_{platform}.tar.gz".format(
        platform = {
            "aarch64-linux": "linux_arm64",
            "aarch64-macos": "darwin_arm64",
            "x86_64-linux": "linux_amd64",
            "x86_64-macos": "darwin_amd64",
        }[exe_platform],
        version = kustomize_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "crictl-" + exe_platform,
    build_file_content = """
exports_files(
    ["crictl"],
    visibility = ["//visibility:public"],
)
""",
    integrity = crictl_integrity[exe_platform],
    url = "https://github.com/kubernetes-sigs/cri-tools/releases/download/v{version}/crictl-v{version}-{platform}.tar.gz".format(
        platform = {
            "aarch64-linux": "linux-arm64",
            "aarch64-macos": "darwin-arm64",
            "x86_64-linux": "linux-amd64",
            "x86_64-macos": "darwin-amd64",
        }[exe_platform],
        version = cri_tools_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "minikube-" + exe_platform,
    build_file_content = """
exports_files(
    ["minikube"],
    visibility = ["//visibility:public"],
)
""",
    integrity = minikube_integrity[exe_platform],
    url = "https://github.com/vimana-cloud/minikube/releases/download/v{version}/minikube-{platform}.tar.gz".format(
        platform = {
            "x86_64-linux": "linux-amd64",
            "x86_64-macos": "darwin-amd64",
        }[exe_platform],
        version = minikube_version,
    ),
) for exe_platform in x86_64_platforms]
