load("@bazel_skylib//lib:selects.bzl", "selects")
load("@bazel_skylib//rules:native_binary.bzl", "native_binary")
load("@rules_shell//shell:sh_binary.bzl", "sh_binary")
load(":private.bzl", "execution_platforms", "format_platform", "format_platform_x86")

exports_files(["test-runner.sh"])

# Execution-platform-specific aliases for pre-built binaries:
# Some custom rules need Bash, so Windows is out for now.
# Pre-built binaries might be available for other CPU's.

[selects.config_setting_group(
    name = "exe-" + platform,
    match_all = [
        "@platforms//cpu:" + platform.split("-")[0],
        "@platforms//os:" + platform.split("-")[1],
    ],
) for platform in execution_platforms]

native_binary(
    name = "kubectl",
    src = format_platform("@kubectl-{}//file"),
    visibility = ["//visibility:public"],
)

native_binary(
    name = "istioctl",
    src = format_platform("@istio-{}//:istioctl"),
    visibility = ["//visibility:public"],
)

native_binary(
    name = "kustomize",
    src = format_platform("@kustomize-{}//:kustomize"),
    visibility = ["//visibility:public"],
)

native_binary(
    name = "crictl",
    src = format_platform("@crictl-{}//:crictl"),
    visibility = ["//visibility:public"],
)

# These binaries are expected to be end up in Docker containers,
# so you probably need the x86-64 / Linux version no matter what the execution platform is:

native_binary(
    name = "crictl-x86_64-linux",
    src = "@crictl-x86_64-linux//:crictl",
    visibility = ["//visibility:public"],
)

native_binary(
    name = "host-local-x86_64-linux",
    src = "@cni-plugins-x86_64-linux//:host-local",
    visibility = ["//visibility:public"],
)

# The native minikube binary has to be wrapped in a script (see below).
native_binary(
    name = "minikube-bin",
    # Minikube does not yet officially support Arm.
    src = format_platform_x86("@minikube-{}//:minikube"),
    visibility = ["//visibility:public"],
)

sh_binary(
    name = "minikube",
    srcs = ["minikube-runner.sh"],
    args = [
        "$(location :minikube-bin)",
        "$(location :kubectl)",
    ],
    data = [
        ":kubectl",
        ":minikube-bin",
    ],
    visibility = ["//visibility:public"],
)

sh_binary(
    name = "tls-generate",
    srcs = ["tls-generate.sh"],
    visibility = ["//visibility:public"],
)
