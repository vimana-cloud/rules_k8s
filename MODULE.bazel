module(
    name = "rules_k8s",
    version = "0.0.1",
)

bazel_dep(name = "buildifier_prebuilt", version = "8.2.0.2", dev_dependency = True, repo_name = "buildtools")

bazel_dep(name = "bazel_skylib", version = "1.8.0")
bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "rules_shell", version = "0.5.0")

# Supported execution platforms for pre-built binaries.
# TODO: Figure out how to dedupe these lists with `private.bzl`.
x86_64_platforms = [
    "x86_64-linux",
    "x86_64-macos",
]

aarch64_platforms = [
    "aarch64-linux",
    "aarch64-macos",
]

execution_platforms = x86_64_platforms + aarch64_platforms

# https://github.com/kubernetes/kubernetes/releases
kubernetes_version = "1.32.3"

kubectl_integrity = {
    "aarch64-linux": "sha384-992AvvKSad0hwynMImFhI9TCD0QB+4QxrCt0Nb13uK0ljkPJYBLAuhY5QIi8Gipu",
    "aarch64-macos": "sha384-4mNi/qqncSSS4iiAysw3Kwq0WkhffD1xkZW3eRi/7OjZ/9lWem1QGyBst+FOKuf/",
    "x86_64-linux": "sha384-PySELntns9AkQ3ZA4Tk74wH5uLuxBy630nbaGVSxaQD5peYL2QMGLHEpSiJqbkl9",
    "x86_64-macos": "sha384-f0L13XaXXmrDv/pzmVlSvratDymq1QJDjA+6VanaTo8MPUL7laxe3gfHeUudUUKK",
}

# https://github.com/istio/istio/releases
istio_version = "1.25.1"

istio_integrity = {
    "aarch64-linux": "sha384-2DvRrxG1+F/eB7NKeS/XBQumAUBysQ+zeP/WrEuYabDxM2tfQpRRRzy2/PCEcV0f",
    "aarch64-macos": "sha384-Iwq7tmlRKrFySCkdIQExirUyQkIAXsSEtBPh7sYGGd2Z992imJK4rwej+VN4bKxg",
    "x86_64-linux": "sha384-DeAYTbdwsyt3SJy6lHph8FOnMMEdZNC09bmyn94YNPA6RqVLX8lF6ZgNQ7/8T+r8",
    "x86_64-macos": "sha384-8NdT/qPRvk0pbxPbvA1Of8v9cmDcFFc6qerM9QN1gT1MdlowgDQr7cptsniJt55X",
}

# https://github.com/containernetworking/plugins/releases
cni_plugins_version = "1.6.2"

# CNI plugins end up in the node Docker image,
# so we only need Linux / x86-64.
cni_plugins_integrity = "sha384-DE4Non3KHJ0T3aLjn0jkdoI33SXnY/4ee6MidCiZDMI/IRtSfQSz7ohqZyuAW/Oi"

# https://github.com/kubernetes-sigs/kustomize/releases
kustomize_version = "5.6.0"

kustomize_integrity = {
    "aarch64-linux": "sha384-LRu1nlTiGZuPqC5Uq7p8C4CgfbieLudwuNBZf0zVtNe8XrQKovr495iRAXvJsW9J",
    "aarch64-macos": "sha384-Yj+2r/vVoIAZNN4X8j2VeE3fGBlZyjTKyPGx8ZESBPqilJcwSaOZ15WUXak55SzK",
    "x86_64-linux": "sha384-tgGdPFRh9am40ZbfDc8FzNJAEy4JHjaRaSOVQfCkuA69LSjj1QozrtSdJR51hahi",
    "x86_64-macos": "sha384-SZPronUSoZCCpLTyr6qJm87nQ/5aV+2pUw7YGWHmqi6rT5SEJS55FXgCr8Dk6B5W",
}

# https://github.com/kubernetes-sigs/cri-tools/releases
cri_tools_version = "1.32.0"

crictl_integrity = {
    "aarch64-linux": "sha384-IdQKFIqnDJf7FW0ghTx/e5C8rEUN/CKrOgUp+i2hRhwuyo8cwP92xzRUPgk099Aa",
    "aarch64-macos": "sha384-F9X0pfy11Sy8eiOuXP8+z8c0dxyKLIZJ0VGTyaERF+2HgKonf5qWBNPtPY+zwBb+",
    "x86_64-linux": "sha384-QeIQzwO9jKnAxfyGfY1caKIr9nX6WpQ9lxNsVHvFag57Pq5W7RUlG5QCKg+czTlC",
    "x86_64-macos": "sha384-1FBOnDDMEP45IH1twFgYW2YdHIcRCseeiEc6cuW7av9P56yO3rJ9xXiWyvXTssxe",
}

# https://github.com/vimana-cloud/minikube/releases
# https://github.com/kubernetes/minikube/releases
minikube_version = "1.36.0-workd"

minikube_integrity = {
    "x86_64-linux": "sha384-qtn6rs3jAV7TH+xSMHgQMVmzqQSGPaP9Zay+YaThfc2gbgAzFGx+2Ytp5aeQ9hur",
    "x86_64-macos": "sha384-08uM+CNK3fJRkZcivvKfEOLEGoqx7KEq0ljI1pp6+Yt4CZO4sHnS6W1Ya0PkagZL",
}

# Manually download prebuilt binaries:

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_file = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_file")

[http_file(
    name = "kubectl-" + exe_platform,
    executable = True,
    integrity = kubectl_integrity[exe_platform],
    url = "https://dl.k8s.io/release/v{version}/bin/{platform}/kubectl".format(
        platform = {
            "aarch64-linux": "linux/arm64",
            "aarch64-macos": "darwin/arm64",
            "x86_64-linux": "linux/amd64",
            "x86_64-macos": "darwin/amd64",
        }[exe_platform],
        version = kubernetes_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "istio-" + exe_platform,
    build_file_content = """
alias(
    name = "istioctl",
    actual = "istio-{version}/bin/istioctl",
    visibility = ["//visibility:public"],
)
""".format(version = istio_version),
    integrity = istio_integrity[exe_platform],
    url = "https://github.com/istio/istio/releases/download/{version}/istio-{version}-{platform}.tar.gz".format(
        platform = {
            "aarch64-linux": "linux-arm64",
            "aarch64-macos": "osx-arm64",
            "x86_64-linux": "linux-amd64",
            "x86_64-macos": "osx-amd64",
        }[exe_platform],
        version = istio_version,
    ),
) for exe_platform in execution_platforms]

http_archive(
    name = "cni-plugins-x86_64-linux",
    build_file_content = """
exports_files(
    ["host-local"],
    visibility = ["//visibility:public"],
)
""",
    integrity = cni_plugins_integrity,
    url = "https://github.com/containernetworking/plugins/releases/download/v{version}/cni-plugins-linux-amd64-v{version}.tgz".format(
        version = cni_plugins_version,
    ),
)

[http_archive(
    name = "kustomize-" + exe_platform,
    build_file_content = """
exports_files(
    ["kustomize"],
    visibility = ["//visibility:public"],
)
""",
    integrity = kustomize_integrity[exe_platform],
    url = "https://github.com/kubernetes-sigs/kustomize/releases/download/kustomize%2Fv{version}/kustomize_v{version}_{platform}.tar.gz".format(
        platform = {
            "aarch64-linux": "linux_arm64",
            "aarch64-macos": "darwin_arm64",
            "x86_64-linux": "linux_amd64",
            "x86_64-macos": "darwin_amd64",
        }[exe_platform],
        version = kustomize_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "crictl-" + exe_platform,
    build_file_content = """
exports_files(
    ["crictl"],
    visibility = ["//visibility:public"],
)
""",
    integrity = crictl_integrity[exe_platform],
    url = "https://github.com/kubernetes-sigs/cri-tools/releases/download/v{version}/crictl-v{version}-{platform}.tar.gz".format(
        platform = {
            "aarch64-linux": "linux-arm64",
            "aarch64-macos": "darwin-arm64",
            "x86_64-linux": "linux-amd64",
            "x86_64-macos": "darwin-amd64",
        }[exe_platform],
        version = cri_tools_version,
    ),
) for exe_platform in execution_platforms]

[http_archive(
    name = "minikube-" + exe_platform,
    build_file_content = """
exports_files(
    ["minikube"],
    visibility = ["//visibility:public"],
)
""",
    integrity = minikube_integrity[exe_platform],
    url = "https://github.com/vimana-cloud/minikube/releases/download/v{version}/minikube-{platform}.tar.gz".format(
        platform = {
            "x86_64-linux": "linux-amd64",
            "x86_64-macos": "darwin-amd64",
        }[exe_platform],
        version = minikube_version,
    ),
) for exe_platform in x86_64_platforms]
