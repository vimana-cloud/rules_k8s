load("@aspect_bazel_lib//lib:run_binary.bzl", "run_binary")
load("@bazel_skylib//lib:selects.bzl", "selects")
load("@bazel_skylib//rules:native_binary.bzl", "native_binary")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@rules_shell//shell:sh_binary.bzl", "sh_binary")
load(":private.bzl", "execution_platforms", "format_platform", "image_export")

exports_files([
    "oras-push.sh",
    "test-runner.sh",
])

# Execution-platform-specific aliases for pre-built binaries:
# Some custom rules need Bash, so Windows is out for now.
# Pre-built binaries might be available for other CPU's.

[selects.config_setting_group(
    name = "exe-" + platform,
    match_all = [
        "@platforms//cpu:" + platform.split("-")[0],
        "@platforms//os:" + platform.split("-")[1],
    ],
) for platform in execution_platforms]

native_binary(
    name = "kubectl",
    src = format_platform("@kubectl.{}//file"),
    visibility = ["//visibility:public"],
)

native_binary(
    name = "kube-apiserver",
    src = format_platform("@kube-apiserver.{}//file"),
    visibility = ["//visibility:public"],
)

native_binary(
    name = "etcd",
    src = format_platform("@etcd.{}//:etcd"),
    visibility = ["//visibility:public"],
)

native_binary(
    name = "etcdctl",
    src = format_platform("@etcd.{}//:etcdctl"),
    visibility = ["//visibility:public"],
)

native_binary(
    name = "etcdutl",
    src = format_platform("@etcd.{}//:etcdutl"),
    visibility = ["//visibility:public"],
)

native_binary(
    name = "helm",
    src = format_platform("@helm.{}//:helm"),
    visibility = ["//visibility:public"],
)

native_binary(
    name = "kustomize",
    src = format_platform("@kustomize.{}//:kustomize"),
    visibility = ["//visibility:public"],
)

native_binary(
    name = "crictl",
    src = format_platform("@crictl.{}//:crictl"),
    visibility = ["//visibility:public"],
)

native_binary(
    name = "kops",
    src = format_platform("@kops.{}//file"),
    visibility = ["//visibility:public"],
)

native_binary(
    name = "operator-sdk",
    src = format_platform("@operator-sdk.{}//file"),
    visibility = ["//visibility:public"],
)

native_binary(
    name = "controller-gen",
    src = format_platform("@controller-gen.{}//file"),
    visibility = ["//visibility:public"],
)

native_binary(
    name = "oras",
    src = format_platform("@oras.{}//:oras"),
    visibility = ["//visibility:public"],
)

native_binary(
    name = "crane",
    src = format_platform("@crane.{}//:crane"),
    visibility = ["//visibility:public"],
)

native_binary(
    name = "kind",
    src = format_platform("@kind.{}//file"),
    visibility = ["//visibility:public"],
)

alias(
    name = "cloud-provider-kind",
    actual = format_platform("@cloud-provider-kind.{}//:cloud-provider-kind"),
    visibility = ["//visibility:public"],
)

native_binary(
    name = "grpcurl",
    src = format_platform("@grpcurl.{}//:grpcurl"),
    visibility = ["//visibility:public"],
)

[native_binary(
    name = "protoc-gen-grpc_{}".format(language),
    src = format_platform("@grpc-plugins.{{}}//:protoc-gen-grpc_{}".format(language)),
    visibility = ["//visibility:public"],
) for language in ["cpp", "csharp", "node", "objective_c", "php", "python", "ruby"]]

alias(
    name = "gateway-api-crds",
    actual = "@gateway-api-crds//file",
    visibility = ["//visibility:public"],
)

alias(
    name = "envoy-gateway-crds",
    actual = "@envoy-gateway-crds//file",
    visibility = ["//visibility:public"],
)

sh_binary(
    name = "image-export",
    srcs = ["image-export.sh"],
)

image_export(
    name = "envoy-gateway-helm-chart",
    image = "@envoy-gateway-helm-image",
    repo_tags = ["docker.io/envoyproxy/gateway-helm:latest"],
    visibility = ["//visibility:public"],
)

image_export(
    name = "cert-manager-helm-chart",
    image = "@cert-manager-helm-image",
    repo_tags = ["quay.io/jetstack/charts/cert-manager:latest"],
    visibility = ["//visibility:public"],
)

# Manually extract this Helm chart until Bazel downloader can handle it.
# https://github.com/helm/helm/issues/31844
# Uses `run_binary` instead of `genrule`
# because the latter cannot handle output directories.
run_binary(
    name = "external-dns-helm-chart",
    tool = ":tar",
    srcs = ["@external-dns-helm-chart//file"],
    args = [
        "-C",
        "$(RULEDIR)",
        "-xzf",
        "$(location @external-dns-helm-chart//file)",
    ],
    out_dirs = ["external-dns"],
    visibility = ["//visibility:public"],
)

# Wrap the system `tar` binary as an executable rule to avoid sourcing a proper toolchain.
# This is only a crutch until the Bazel downloader can handle Helm charts anyway.
# And everybody should have `tar` installed on the PATH, right?
write_file(
    name = "tar-wrapper",
    out = "tar-wrapper.sh",
    content = [
        "#!/usr/bin/env bash",
        "exec tar \"$@\"",
    ],
    is_executable = True,
)

sh_binary(
    name = "tar",
    srcs = [":tar-wrapper"],
)
